#pragma kernel density

RWStructuredBuffer<float4> points;
int numPointAxis;

float3 playerSpawn;
float3 endZone;

//world position of the chunk 
float3 chunkPos;

//coord of the chunk
float3 chunkCoord;

uint indexFromCoord(uint3 id) {
    return id.z * numPointAxis * numPointAxis + id.y * numPointAxis + id.x;
}

float3 hash(float3 x )
{
	x = float3( dot(x,float3(127.1,311.7, 74.7)),
			  dot(x,float3(269.5,183.3,246.1)),
			  dot(x,float3(113.5,271.9,124.6)));

	return frac(sin(x)*43758.5453123);
}

[numthreads(8,8,8)]
void density (uint3 id : SV_DispatchThreadID)
{
    if(id.x >= numPointAxis || id.y >= numPointAxis || id.z >= numPointAxis)
    {
        return;
    }


	///////// worley noise /////////

    float3 pos = chunkCoord + (float3(id)/float(numPointAxis-1.));

	float dist = 1.;

	for(float i = -1.; i <=1. ; i++)
	{
		for(float j = -1.; j <=1. ; j++)
		{
			for(float k = -1.; k <=1. ; k++) 
			{

				float3 loopPos = float3(i, j, k);
				float temp = distance(pos + loopPos,
									chunkCoord + hash(chunkCoord - loopPos)
									);
				if(temp < dist)
					dist = temp;

			}
		}
	}

	float densityValue = 1.-dist;

	//get the pos
	uint index = indexFromCoord(id);


    points[index] = float4(id, densityValue);
	pos = chunkPos + id;

	///////   special gen area (spawn, tunnel, hardfloor and boss area)    ////////

	float sphereStart = distance(playerSpawn, pos);

	if (sphereStart < 20.)
	{
		points[index] = float4(id, sphereStart - 20.);
		return;
	}

	float sphereEnd = distance(endZone, pos);
	if (sphereEnd < 50)
	{
		points[index] = float4(id, sphereEnd - 50.);
		return;
	}

	float tube1 = distance(pos, float3(sin(pos.z / 10.) * 6., cos(pos.z / 10.) * 6. - pos.z / 6., pos.z));
    float tube2 = distance(pos, float3(cos(pos.z / 10.) * 6., sin(pos.z / 10.) * 6. - pos.z / 6., pos.z));

	if (tube1 < 9. && densityValue > tube1 - 9.)
	{
        points[index] = float4(id, tube1 - 9.);
		return;
	}
	if (tube2 < 9. && densityValue > tube2 - 9.)
	{
		points[index] = float4(id, tube2 - 9.);
		return;
	}

	if(pos.y < -100)
		points[index] = float4(id, 1.); 
}
